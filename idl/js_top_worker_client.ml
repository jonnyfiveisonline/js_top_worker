(** Worker rpc *)

(** Functions to facilitate RPC calls to web workers. *)

module Worker = Brr_webworkers.Worker
open Brr_io
open Js_top_worker_rpc

(** The assumption made in this module is that RPCs are answered in the order
    they are made. *)

type context = {
  worker : Worker.t;
  timeout : int;
  timeout_fn : unit -> unit;
  waiting : ((Rpc.response, exn) Result.t Lwt_mvar.t * int) Queue.t;
}

type rpc = Rpc.call -> Rpc.response Lwt.t

exception Timeout

(* let log s = Js_of_ocaml.Firebug.console##log (Js_of_ocaml.Js.string s) *)

let demux context msg =
  Lwt.async (fun () ->
      match Queue.take_opt context.waiting with
      | None -> Lwt.return ()
      | Some (mv, outstanding_execution) ->
          Brr.G.stop_timer outstanding_execution;
          let msg = Message.Ev.data (Brr.Ev.as_type msg) in
          Js_of_ocaml.Console.console##log
            (Js_of_ocaml.Js.string
               "Client received the following, to be converted to an OCaml \
                string");
          Js_of_ocaml.Console.console##log msg;
          let msg = Js_of_ocaml.Js.to_string msg in
          (* log (Printf.sprintf "Client received: %s" msg); *)
          Lwt_mvar.put mv (Ok (Jsonrpc.response_of_string msg)))

let rpc : context -> Rpc.call -> Rpc.response Lwt.t =
 fun context call ->
  let open Lwt in
  let jv = Jsonrpc.string_of_call call |> Js_of_ocaml.Js.string in
  (* log (Printf.sprintf "Client sending: %s" jv); *)
  let mv = Lwt_mvar.create_empty () in
  let outstanding_execution =
    Brr.G.set_timeout ~ms:context.timeout (fun () ->
        Lwt.async (fun () -> Lwt_mvar.put mv (Error Timeout));
        Worker.terminate context.worker;
        context.timeout_fn ())
  in
  Queue.push (mv, outstanding_execution) context.waiting;
  Worker.post context.worker jv;
  Lwt_mvar.take mv >>= fun r ->
  match r with
  | Ok jv ->
      let response = jv in
      Lwt.return response
  | Error exn -> Lwt.fail exn

let start url timeout timeout_fn : rpc =
  let worker = Worker.create (Jstr.v url) in
  let context = { worker; timeout; timeout_fn; waiting = Queue.create () } in
  let _listener =
    Brr.Ev.listen Message.Ev.message (demux context) (Worker.as_target worker)
  in
  rpc context

module Rpc_lwt = Idl.Make (Lwt)
module Wraw = Toplevel_api_gen.Make (Rpc_lwt.GenClient ())

module W : sig
  type init_config = Toplevel_api_gen.init_config
  type err = Toplevel_api_gen.err
  type exec_result = Toplevel_api_gen.exec_result

  val init :
    rpc ->
    Toplevel_api_gen.init_config ->
    (unit, Toplevel_api_gen.err) result Lwt.t

  val setup :
    rpc ->
    unit ->
    (Toplevel_api_gen.exec_result, Toplevel_api_gen.err) result Lwt.t

  val typecheck :
    rpc ->
    string ->
    (Toplevel_api_gen.exec_result, Toplevel_api_gen.err) result Lwt.t

  val exec :
    rpc ->
    string ->
    (Toplevel_api_gen.exec_result, Toplevel_api_gen.err) result Lwt.t

  val query_errors :
    rpc ->
    string option ->
    string list ->
    bool ->
    string ->
    (Toplevel_api_gen.error list, Toplevel_api_gen.err) result Lwt.t

  val compile_js :
    rpc ->
    string option ->
    string ->
    (string, Toplevel_api_gen.err) result Lwt.t
end = struct
  type init_config = Toplevel_api_gen.init_config
  type err = Toplevel_api_gen.err
  type exec_result = Toplevel_api_gen.exec_result

  let init rpc a = Wraw.init rpc a |> Rpc_lwt.T.get
  let setup rpc a = Wraw.setup rpc a |> Rpc_lwt.T.get
  let typecheck rpc a = Wraw.typecheck rpc a |> Rpc_lwt.T.get
  let exec rpc a = Wraw.exec rpc a |> Rpc_lwt.T.get

  let query_errors rpc id deps is_toplevel doc =
    Wraw.query_errors rpc id deps is_toplevel doc |> Rpc_lwt.T.get

  let compile_js rpc id s = Wraw.compile_js rpc id s |> Rpc_lwt.T.get
end
